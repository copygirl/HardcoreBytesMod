buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"


version = mod_version
group   = "net.mcft.copy.hardcorebytesmod"
archivesBaseName = "HardcoreBytesMod-$mc_version"

// If building snapshot version, append "-SNAPSHOT+<commit_hash> to version.
if (is_snapshot) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    version = "$version-SNAPSHOT+${stdout.toString().trim()}"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava { sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8 }

minecraft {
    version  = forge_version
    mappings = mcp_mappings
    runDir   = "run"

    replace "@VERSION@", project.version
}

repositories {
    maven {
        name "jared maven"
        url "http://blamejared.com/maven"
    }
}

dependencies {
    deobfCompile "blusunrize:ImmersiveEngineering:$ie_version-+"
    compile project(":athenaeum")
    compile project(":artisan-worktables")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
